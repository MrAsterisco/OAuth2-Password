apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'

def libVersion = "3.1.5"

static def getTimestamp() {
    def currentDate = new Date()
    def formattedDate = currentDate.format('yyMMddHHmm')
    def code = formattedDate.toInteger()
    return code
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode getTimestamp()
        versionName libVersion

        consumerProguardFiles 'proguard-rules.txt'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    testOptions {
        unitTests.all {
            // All the usual Gradle options.
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen {false}
                showStandardStreams = true
            }
        }
        testOptions {
            unitTests.returnDefaultValues = true
        }
    }
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId 'com.pillohealth'
            version = libVersion
            artifactId project.getName()
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
        }
    }

    repositories {
        maven {
            url azureArtifactsFeedURL
            credentials {
                username "AZURE_ARTIFACTS"
                password System.getenv("AZURE_ARTIFACTS_ENV_ACCESS_TOKEN") ?: "${azureArtifactsGradleAccessToken}"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation 'com.squareup.okhttp3:okhttp:3.13.1'
    implementation 'com.google.code.gson:gson:2.8.5'

    testImplementation 'junit:junit:4.13'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.4.0"
}
